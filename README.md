# Nixcraft (WIP)

Nixcraft - A declarative minecraft launcher in nix

Warning - This project is in a usable state but still a work in progress. Do expect things to break.

## Features

  1. Supports clients and servers
  2. Supports Mod Loaders (Fabric loader, Quilt loader & paper servers)
  3. Supports Modpacks (modrinth .mrpack)
  4. MCSR - Supports speedrunning related content (waywall integration)
  5. Microsoft Authentication support via `nixcraft-auth`
  6. Skin and cape management via `nixcraft-skin`
  7. CLI launcher with logging via `nixcraft` command
  8. Custom waywall and GLFW path support for speedrunning
  9. Desktop entry icons
  10. LWJGL version override (e.g., 3.3.3 for speedrunning)
  11. MangoHud overlay support
  12. Custom wrapper commands (gamemoderun, etc.)
  13. Discrete GPU selection (NVIDIA/AMD)
  14. Automatic skin caching

## CLI Tools

Nixcraft provides several command-line tools:

### nixcraft (CLI Launcher)

Launch instances with terminal output and automatic logging.

```bash
nixcraft <instance>        # Launch an instance with logging
nixcraft list              # List available instances
nixcraft logs <instance>   # View logs for an instance

# Examples
nixcraft 1.21.1            # Launch your 1.21.1 instance
nixcraft Ranked            # Launch your MCSR Ranked instance
```

Logs are saved to `~/.local/share/nixcraft/logs/<instance>/<timestamp>.log`

### nixcraft-auth (Microsoft Authentication)

Authenticate your Microsoft/Minecraft account for online play.

```bash
nixcraft-auth login        # Login via Microsoft OAuth
nixcraft-auth status       # Check authentication status
nixcraft-auth refresh      # Refresh access token
nixcraft-auth logout       # Remove stored credentials
nixcraft-auth token-path   # Show path to access token file
```

### nixcraft-skin (Skin & Cape Manager)

Manage Minecraft skins and capes.

```bash
nixcraft-skin fetch <username>           # Download a player's skin
nixcraft-skin fetch-capes <username>     # Download a player's cape
nixcraft-skin apply <file> [--variant]   # Apply a skin to your account
nixcraft-skin list [--capes]             # List saved skins/capes
nixcraft-skin my-capes                   # List your owned capes
nixcraft-skin set-cape <id-or-name>      # Set active cape (or 'none')
nixcraft-skin info                       # Show directory paths
```

Skins are saved to `~/.local/share/nixcraft/skins/`
Capes are saved to `~/.local/share/nixcraft/skins/capes/`

## TODO

  1. Support Forge
  2. Support more modpacks such as packwiz

## Usage

You can run a minecraft instance right off the bat.

Example one-liner commands to run in the current dir

### server

```sh
nix run --impure --expr '(builtins.getFlake "github:loystonpais/nixcraft").outputs.packages.x86_64-linux.server.override { cfg = { version = "1.21.1"; absoluteDir = builtins.getEnv "PWD"; agreeToEula = true; }; }'
```

### client

```sh
nix run --impure --expr '(builtins.getFlake "github:loystonpais/nixcraft").outputs.packages.x86_64-linux.client.override { cfg = { version = "1.16.1"; account = {  }; absoluteDir = builtins.getEnv "PWD"; }; }'
```

## Usage (nix profile)

This in my opinion is the best way to install a nixcraft instance.

Pros:

  1. Easy to use and convenient
  2. Automatically adds binary and desktop item to path
  3. No nixos rebuilding required
  4. Not garbage collected until removed
  5. No root needed

Here's how you would install a client. Same applies for a server instance.

```nix
nix profile add --impure --expr '
(builtins.getFlake "github:loystonpais/nixcraft").outputs.packages.x86_64-linux.client.override {
  name = "profile-demo";
  cfg = {
    version = "1.16.1";
    account = {};
    desktopEntry.name = "Nixcraft Profile Demo";
    absoluteDir = "${builtins.getEnv "HOME"}/profile-demo"; # game dir set to $HOME/profile-demo
  };
}
'
```

Upon running the above command a package `profile-demo` will be added to your profile. To remove it, simply run `nix profile remove profile-demo`.

Checkout the home-manager usage for additional configuration options.

## Usage (home-manager)

Nixcraft module can be integrated with home-manager and nixos modules.

See `docs` for all available options.

```nix
# in flake.nix inputs add
nixcraft = {
    url = "github:loystonpais/nixcraft";
    inputs.follows.nixpkgs = "nixpkgs"; # Set correct nixpkgs name
};

```

```nix
# Config showcasing nixcraft's features
{
  config,
  pkgs,
  lib,
  inputs,
  ...
}: let
  # Fetch any mrpack which can be used with both servers and clients!
  simply-optimized-mrpack = pkgs.fetchurl {
    url = "https://cdn.modrinth.com/data/BYfVnHa7/versions/vZZwrcPm/Simply%20Optimized-1.21.1-5.0.mrpack";
    hash = "sha256-n2BxHMmqpOEMsvDqRRYFfamcDCCT4ophUw7QAJQqXmg=";
  };
in {
  imports = [
    # Import the nixcraft home module
    inputs.nixcraft.homeModules.default
  ];

  config = {
    nixcraft = {
      /*
      * Options starting with underscore such as _clientSettings are for advanced use case
      * Most instance options (such as java, mod loaders) are generic. There are also client/server specific options
      * Options are mostly inferred to avoid duplication.
        Ex: minecraft versions and mod loader versions are automatically inferred if mrpack is set

      * Instances are placed under ~/.local/share/nixcraft/client/instances/<name> or ~/.local/share/nixcraft/server/instances/<name>

      * Executable to run the instance will be put in path as nixcraft-<server/client>-<name>
      * Ex: nixcraft-client-myclient
      * See the binEntry option for customization

      * Read files found under submodules for more options
      * Read submodules/genericInstanceModule.nix for generic options
      */

      enable = true;

      server = {
        # Config shared with all instances
        shared = {
          agreeToEula = true;
          serverProperties.online-mode = false;

          binEntry.enable = true;
        };

        instances = {
          # Example server with bare fabric loader
          smp = {
            enable = true;
            version = "1.21.1";
            fabricLoader = {
              enable = true;
              version = "0.17.2";
            };
          };

          # Example server with simply-optimized mrpack loaded
          simop = {
            enable = true;
            mrpack = {
              enable = true;
              file = simply-optimized-mrpack;
            };
            java.memory = 2000;
            serverProperties = {
              level-seed = "6969";
              online-mode = false;
              bug-report-link = null;
            };
            # servers can be run as systemd user services
            # service name is set as nixcraft-server-<name>.service
            service = {
              enable = true;
              autoStart = false;
            };
          };

          # Example paper server
          paper-server = {
            version = "1.21.1";
            enable = true;
            paper.enable = true;
            java.memory = 2000;
            serverProperties.online-mode = false;
          };

          onepoint5 = {
            enable = true;
            version = "1.5.1";
          };

          onepoint8 = {
            enable = true;
            version = "1.8";
          };

          onepoint12 = {
            version = "1.12.1";
            enable = true;
            agreeToEula = true;
            # Old versions fail to start if server poperties is immutable
            # So copy the file instead
            files."server.properties".method = lib.mkForce "copy";
            binEntry.enable = true;
          };

          # Example server with quilt loader
          quilt-server = {
            enable = true;
            version = "1.21.1";
            quiltLoader = {
              enable = true;
              version = "0.29.1";
            };
          };
        };
      };

      client = {
        # Config to share with all instances
        shared = {
          # Symlink screenshots dir from all instances
          files."screenshots".source = config.lib.file.mkOutOfStoreSymlink "${config.home.homeDirectory}/Pictures";

          # Common account
          account = {
            username = "loystonlive";
            uuid = "2909ee95-d459-40c4-bcbb-65a0cc413110";
            offline = true;
          };

          useDiscreteGPU = true; # Enabled by default

          # Game is passed to the gpu (set if you have nvidia gpu)
          enableNvidiaOffload = true; # Enabled by default

          envVars = {
            # Fixes bug with nvidia (applied by default)
            __GL_THREADED_OPTIMIZATIONS = "0";
          };

          binEntry.enable = true;
        };

        instances = {
          # Example instance with simply-optimized mrpack
          simop = {
            enable = true;

            # Add a desktop entry
            desktopEntry = {
              enable = true;
            };
            mrpack = {
              enable = true;
              file = simply-optimized-mrpack;
            };
          };

          # Example bare bones client
          nomods = {
            enable = true;
            version = "1.21.1";
          };

          # Example client whose version is "latest-release"
          # Supports "latest-snapshot" too
          latest = {
            enable = true;
            version = "latest-release";
          };

          # Audio doesn't seem to work in old versions
          onepoint5 = {
            enable = true;
            version = "1.5.1";
          };

          onepoint8 = {
            enable = true;
            version = "1.8";
          };

          onepoint12 = {
            enable = true;
            version = "1.12.1";
          };

          # Example client with quilt loader
          quilt-client = {
            enable = true;
            version = "1.21.1";
            quiltLoader = {
              enable = true;
              version = "0.29.1";
            };
          };

          # Example client customized for minecraft speedrunning
          fsg = {
            enable = true;

            # this advanced option accepts common arguments that are passed to the client
            _classSettings = {
              fullscreen = true;
              # height = 1080;
              # width = 1920;
              uuid = "2909ee95-d459-40c4-bcbb-65a0cc413110";
              username = "loystonlive";
            };
            # version = "1.16.1"; # need not be set (inferred)

            mrpack = {
              enable = true;
              file = pkgs.fetchurl {
                url = "https://cdn.modrinth.com/data/1uJaMUOm/versions/jIrVgBRv/SpeedrunPack-mc1.16.1-v5.3.0.mrpack";
                hash = "sha256-uH/fGFrqP2UpyCupyGjzFB87LRldkPkcab3MzjucyPQ=";
              };
            };

            # Set custom world saves
            saves = {
              "Practice Map" = pkgs.fetchzip {
                url = "https://github.com/Dibedy/The-MCSR-Practice-Map/releases/download/1.0.1/MCSR.Practice.v1.0.1.zip";
                stripRoot = false;
                hash = "sha256-ukedZCk6T+KyWqEtFNP1soAQSFSSzsbJKB3mU3kTbqA=";
              };
            };

            # place custom files
            files = {
              # mods can also be manually set
              "mods/fsg-mod.jar".source = pkgs.fetchurl {
                url = "https://cdn.modrinth.com/data/XZOGBIpM/versions/TcTlTNlF/fsg-mod-5.1.0%2BMC1.16.1.jar";
                hash = "sha256-gQfbJMsp+QEnuz4T7dC1jEVoGRa5dmK4fXO/Ea/iM+A=";
              };

              # setting config files
              "config/mcsr/standardsettings.json".source = ./standardsettings.json;
              "options.txt" = {
                source = ./options.txt;
              };
            };

            java = {
              extraArguments = [
                "-XX:+UseZGC"
                "-XX:+AlwaysPreTouch"
                "-Dgraal.TuneInlinerExploration=1"
                "-XX:NmethodSweepActivity=1"
              ];
              # override java package. This mrpack needs java 17
              package = pkgs.jdk17;
              # set memory in MBs
              maxMemory = 3500;
              minMemory = 3500;
            };

            # waywall can be enabled
            waywall.enable = true;

            # Add executable to path
            binEntry = {
              enable = true;
              # Set executable name
              name = "fsg";
            };

            desktopEntry = {
              enable = true;
              name = "Nixcraft FSG";
              extraConfig = {
                # TODO: fix icons not working
                # icon = "${inputs.self}/assets/minecraft/dirt.svg";
                terminal = true;
              };
            };
          };

          # ============================================================
          # MCSR Ranked Example - Full featured speedrunning setup
          # ============================================================
          ranked = {
            enable = true;

            mrpack = {
              enable = true;
              file = pkgs.fetchurl {
                url = "https://redlime.github.io/MCSRMods/modpacks/v4/MCSRRanked-Linux-1.16.1-All.mrpack";
                hash = "sha256-mPerodqNklfLpeNzcJqe5g/wf9mGFwPNl7vApl/RggI=";
              };
            };

            # Override LWJGL version (required for some speedrunning setups)
            lwjglVersion = "3.3.3";

            # Enable MangoHud overlay for FPS/performance monitoring
            mangohud.enable = true;
            # mangohud.configFile = ./mangohud.conf;  # Optional custom config

            java = {
              package = pkgs.jdk17;
              maxMemory = 4096;
              minMemory = 512;
            };

            # Waywall with custom command for GPU selection - When using full paths you must rebiuld with --impure 
            waywall = {
              enable = true;
              # Simple path (auto-adds "wrap --")
              binaryPath = "/path/to/waywall";
              glfwPath = "/path/to/libglfw.so";

              # OR full custom command as list (you control everything)
              # command = ["env" "DRI_PRIME=renderD128" "/path/to/waywall" "wrap" "--"];
            };

            # Microsoft account authentication (use nixcraft-auth to login)
            account = {
              username = "YourUsername";
              accessTokenPath = "/home/user/.local/share/nixcraft/auth/access_token"; # When using full paths you must rebiuld with --impure 
              # Skin auto-applied before each launch
              skin = {
                file = /path/to/skin.png;
                variant = "classic";  # or "slim"
              };
            };

            binEntry = {
              enable = true;
              name = "ranked";
            };

            desktopEntry = {
              enable = true;
              name = "MCSR Ranked";
              icon = /path/to/icon.png;
            };
          };

          # ============================================================
          # RSG (Random Seed Glitchless) Example
          # ============================================================
          rsg = {
            enable = true;

            mrpack = {
              enable = true;
              file = pkgs.fetchurl {
                url = "https://cdn.modrinth.com/data/1uJaMUOm/versions/jIrVgBRv/SpeedrunPack-mc1.16.1-v5.3.0.mrpack";
                hash = "sha256-uH/fGFrqP2UpyCupyGjzFB87LRldkPkcab3MzjucyPQ=";
              };
            };

            # LWJGL 3.3.3 for better performance
            lwjglVersion = "3.3.3";

            # MangoHud for monitoring
            mangohud.enable = true;

            # Custom wrapper command (e.g., gamemoderun)
            # wrapper = ["gamemoderun"];
            # wrapper = "/usr/bin/gamemoderun";  # String form adds "wrap --"

            # Custom environment variables
            envVars = {
              SOME_VAR = "value";
            };

            # GPU Selection
            useDiscreteGPU = true;        # Enable discrete GPU (default: true)
            # enableDriPrime = true;      # AMD/Intel: sets DRI_PRIME=1
            # enableNvidiaOffload = true; # NVIDIA: sets offload env vars

            java = {
              package = pkgs.graalvmPackages.graalvm-oracle_17;
              maxMemory = 14000;
              minMemory = 11500;
              extraArguments = [
                "-XX:+UnlockExperimentalVMOptions"
                "-XX:+UseZGC"
                "-XX:+AlwaysPreTouch"
              ];
            };

            # Waywall for wall-style resets
            waywall = {
              enable = true;
              binaryPath = "/path/to/waywall"; # When using full paths you must rebiuld with --impure 
              glfwPath = "/path/to/libglfw.so"; # When using full paths you must rebiuld with --impure 
              # profile = "my-profile";       # Optional waywall profile
              # configDir = /path/to/config;  # Custom config directory
              # configText = "lua code here"; # Inline lua config
            };

            account = {
              username = "YourUsername";
              accessTokenPath = "/home/user/.local/share/nixcraft/auth/access_token";  # When using full paths you must rebiuld with --impure 
            };

            binEntry = {
              enable = true;
              name = "rsg";
            };

            desktopEntry = {
              enable = true;
              name = "SeedQueue RSG";
              icon = /path/to/icon.png;
            };
          };

          # ============================================================
          # Simple client with MangoHud only
          # ============================================================
          casual = {
            enable = true;
            version = "1.21.1";

            # Just enable MangoHud overlay
            mangohud.enable = true;

            # Use a wrapper like gamemoderun
            wrapper = ["gamemoderun"];

            binEntry.enable = true;
          };
        };
      };
    };
  };
}
```
